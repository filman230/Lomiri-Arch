# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=qtmir-git
_pkgname=qtmir
pkgver=r2313.b37293e2
pkgrel=1
pkgdesc='Qt platform abstraction (QPA) plugin for a Mir server'
url='https://github.com/ubports/qtmir'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
depends=(qt5-base qt5-quickcontrols qt5-sensors mir process-cpp ubuntu-app-launch-git gsettings-qt libqtdbustest-git libqtdbusmock-git content-hub-git url-dispatcher-git glm gtest)
makedepends=(git cmake cmake-extras-git)
source=('git+https://github.com/ubports/qtmir.git'
        'NoCatchValue.patch')
sha256sums=('SKIP'
            '0e706edbd8ef0bc7358202b01c6efcba1d2837699457628d9ec942e6a5135a4e')

BUILD_DIR=build

prepare() {
  cd ${_pkgname}
  git checkout bionic

  patch -Np1 -i "${srcdir}/NoCatchValue.patch"
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" ..
  make
}

check() {
  cd ${_pkgname}/${BUILD_DIR}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
