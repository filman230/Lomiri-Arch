# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=unity-api-git
_pkgname=unity-api
pkgver=r1088.31cad5a
pkgrel=1
pkgdesc='API for Unity shell integration'
url='https://github.com/ubports/unity-api'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
depends=(qt5-quickcontrols)
makedepends=(git cmake cppcheck graphviz libqtdbustest-git )
source=('git+https://github.com/ubports/unity-api.git')
sha256sums=('SKIP')

BUILDENV+=('!check') # Some tests are broken

BUILD_DIR=build

prepare() {
  cd ${_pkgname}
  git checkout bionic
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DNO_TESTS=true ..
  make
}

check() {
  cd ${_pkgname}/${BUILD_DIR}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
