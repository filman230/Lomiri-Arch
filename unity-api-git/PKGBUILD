# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=unity-api-git
_pkgname=unity-api
pkgver=b56d9b0
pkgrel=2
pkgdesc='API for Unity shell integration'
url='https://github.com/ubports/unity-api'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
depends=(qt5-quickcontrols)
makedepends=(git cmake libqtdbustest-git)
source=('git+https://github.com/ubports/unity-api.git'
        'Qt511.patch')
sha256sums=('SKIP'
            '1abc61b62c3e0e2110b6a765064b7a73b8c6bfa7f24ad0065bea940140810be5')

BUILD_DIR=build

prepare() {
  cd ${_pkgname}
  git checkout bionic
  patch -Np1 -i "${srcdir}/Qt511.patch"
}

pkgver() {
  cd ${_pkgname}
  git describe --always
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" ..
  make
}

package() {
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
