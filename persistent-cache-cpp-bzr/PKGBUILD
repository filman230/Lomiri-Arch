# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=persistent-cache-cpp-bzr
_pkgname=persistent-cache-cpp
pkgver=r28
pkgrel=2
pkgdesc='A cache of key-value pairs with persistent storage for C++'
url='https://launchpad.net/persistent-cache-cpp'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(persistent-cache-cpp)
provides=(persistent-cache-cpp)
depends=(boost leveldb valgrind)
makedepends=(bzr cmake cmake-extras-git doxygen graphviz lcov gcovr gtest)
source=("bzr+lp:${_pkgname}")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  echo "r$(bzr revno)"
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
