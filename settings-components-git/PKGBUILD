# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=settings-components-git
_pkgname=settings-components
pkgver=886f6de
pkgrel=1
pkgdesc='Ubuntu settings components for Unity8'
url='https://github.com/ubports/settings-components'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL GPL)
depends=(xorg-server-xvfb qt5-quickcontrols)
makedepends=(git cmake cmake-extras-git)
source=('git+https://github.com/ubports/settings-components.git'
        'Qt511.patch')
sha256sums=('SKIP'
            '24ec72e13cdcbc9d9f3ffe71b951340aa5eb7f9bc2fda58d2d46c6e94404d32d')

BUILDENV+=('!check') # https://paste.ubuntu.com/p/gsfv2YX6Z5/
BUILD_DIR=build

prepare() {
  cd ${_pkgname}
  git checkout bionic
  
  patch -Np1 -i "${srcdir}/Qt511.patch"
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" ..
  make
}

check() {
  cd ${_pkgname}/${BUILD_DIR}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
