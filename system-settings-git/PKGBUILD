# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=system-settings-git
_pkgname=system-settings
pkgver=3b26947f
pkgrel=1
pkgdesc='System Settings application for Unity8'
url='https://github.com/ubports/system-settings'
arch=(x86_64 i686 armv7h aarch64)
license=(GPL3)
depends=(trust-store-git click-git gnome-desktop ubuntu-app-launch-git geonames-git libqtdbusmock-git ubuntu-ui-toolkit-git gsettings-qt accountsservice-ubuntu upower)
makedepends=(git cmake intltool)
checkdepends=(xorg-server-xvfb)
source=('git+https://github.com/ubports/system-settings.git'
        'Qt511.patch')
sha256sums=('SKIP'
            '554e10d884cee052893a64db9f90dd846ca20f716c7fbd75140af4ea5d197d27')

BUILDENV+=('!check') # Some tests fail

pkgver() {
  cd ${_pkgname}
  git describe --always
}

prepare() {
  cd ${_pkgname}
  git checkout bionic

  patch -Np1 -i "${srcdir}/Qt511.patch"
  sed -i 's/update.cpp//g' plugins/system-update/CMakeLists.txt
  sed -i 's/apt-pkg//g' plugins/system-update/CMakeLists.txt
  sed -i 's/apt-pkg//g' tests/mocks/Ubuntu/SystemSettings/Update/CMakeLists.txt
  echo 'message("No updates sorry")' > tests/plugins/system-update/CMakeLists.txt
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" ..
  make
}

check() {
  cd ${_pkgname}/build
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
