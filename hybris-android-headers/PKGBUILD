# Maintainer: Ivan Semkin (ivan at semkin dot ru)
# Contributor MichaÃ«l Serpieri <mickybart@pygoscelis.org>

pkgname=hybris-android-headers
_pkgname=android-headers
pkgver=5.1.1
pkgrel=1
pkgdesc='Header files needed to write applications for the Android platform'
url='https://github.com/Halium/android-headers'
arch=(x86_64 i686 armv7h aarch64)
license=(custom)
provides=(hybris-android-headers)
conflicts=(hybris-android-headers)
source=('git+https://github.com/Halium/android-headers.git')
sha256sums=('SKIP')
options=('!strip')

package() {
  cd ${_pkgname}
  install -d "${pkgdir}/opt/android"

  sed '/\/\* CONFIG GOES HERE/,$d' android-config.h > android-config-caf.h
  cat <<EOF >> android-config-caf.h
#define QCOM_BSP 1
#define QTI_BSP 1
EOF
  sed '0,/\/\* CONFIG GOES HERE/d' android-config.h >> android-config-caf.h

  make DESTDIR="${pkgdir}/" PREFIX="/opt/android" INCLUDEDIR="/opt/android/include" PKGCONFIGDIR="/usr/lib/pkgconfig" install
  sed -i 's|Cflags:.*|Cflags: -I${includedir}|' "${pkgdir}/usr/lib/pkgconfig/android-headers.pc"

  cp android-config.h ${pkgdir}/opt/android/include/android-config-generic.h
  cp android-config-caf.h android-version.h ${pkgdir}/opt/android/include
}
# vim:set ts=2 sw=2 et:
