# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=ubuntu-ui-toolkit-git
_pkgname=ubuntu-ui-toolkit
pkgver=de9a0b250
pkgrel=1
pkgdesc='Qt Components for Ubuntu'
url='https://github.com/ubports/ubuntu-ui-toolkit'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
depends=(qt5-base qt5-quickcontrols lttng-ust libnih qt5-systems-git qt5-pim-git qt5-svg qt5-feedback)
makedepends=(git)
checkdepends=()
source=('git+https://github.com/ubports/ubuntu-ui-toolkit.git'
        'NoWarns.patch'
        'QTest.patch'
        'dpkg.patch'
        'NoDocs.patch')
sha256sums=('SKIP'
            '3b8a3165161d5c695a7f3f8f3b08a3de11451b8f52276b5b1856730f0aa557c9'
            '09265797bb847baedc2d7a19968e853d487d63eec538e3be93fc706ff40598d3'
            '5851aec4b72ddd1556494ab188c6de64ccaa77a1b574a0bd2631916300ac242a'
            '7b3e6078815dac602792211e69d7ffd878056ecdb96b19f221db67e6e5ac4614')

BUILDENV+=('!check') # It needs a DISPLAY, and I am too tired to set it up now

prepare() {
  cd ${_pkgname}
  git checkout bionic
  
  patch -Np1 -i "${srcdir}/NoWarns.patch"
  patch -Np1 -i "${srcdir}/QTest.patch"
  patch -Np1 -i "${srcdir}/dpkg.patch"
  patch -Np1 -i "${srcdir}/NoDocs.patch"
}

pkgver() {
  cd ${_pkgname}
  git describe --always
}

build() {
  cd ${_pkgname}
  qmake
  make
}

check() {
  cd ${_pkgname}
  make check TESTARGS="-silent"
}

package() {
  cd ${_pkgname}
  mkdir -p /tmp/runtime-builduser
  XDG_RUNTIME_DIR=/tmp/runtime-builduser make INSTALL_ROOT="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
