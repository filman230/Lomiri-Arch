# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=indicator-network-git
_pkgname=indicator-network
pkgver=r1947.870dd8a2
pkgrel=1
pkgdesc='Network indicator'
url='https://github.com/ubports/indicator-network'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3 GPL3)
depends=(lcov gcovr gsettings-qt ofono url-dispatcher-git libqofono libqtdbustest-git libqtdbusmock-git gmenuharness-bzr)
makedepends=(git cmake cmake-extras-git doxygen)
checkdepends=()
source=('git+https://github.com/ubports/indicator-network.git'
        'qt511.patch')
sha256sums=('SKIP'
            '30ad14aae26baae665d6cc095e71ae3f71d8798fbfa82f91effb62507f865196')

BUILDENV+=('!check') # Hangs

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
  cd ${_pkgname}
  git checkout bionic

  patch -Np1 -i "${srcdir}/qt511.patch"
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" ..
  make
}

check() {
  cd ${_pkgname}/build
  export PYTHONIOENCODING=UTF-8
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
