# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=ubuntu-download-manager-git
_pkgname=ubuntu-download-manager
pkgver=e6cfc9dc
pkgrel=2
pkgdesc='Provides a service for downloading files while an application is suspended'
url='https://github.com/ubports/ubuntu-download-manager'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
depends=(boost dbus-test-runner gmock qt5-base qt5-declarative libnih google-glog)
makedepends=(git doxygen cmake)
source=('git+https://github.com/ubports/ubuntu-download-manager.git'
        'QtArg.patch'
        'Gtest.patch')
sha256sums=('SKIP'
            '8182121308e04f60e29dfc1e3c8e221413b5a905ea1302e75acbe95a6bb7f866'
            '7c8fda70b365baeb48b96dd031ae8efff1f18161031f847190bc0fc42d3d206b')

BUILDENV+=('!check') # Some tests are broken

prepare() {
  cd ${_pkgname}
  git checkout bionic
  
  patch -Np1 -i "${srcdir}/QtArg.patch"
  patch -Np1 -i "${srcdir}/Gtest.patch"
}

pkgver() {
  cd ${_pkgname}
  git describe --always
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
