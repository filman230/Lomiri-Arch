# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=content-hub-git
_pkgname=content-hub
pkgver=66955f6
pkgrel=1
pkgdesc='Content sharing/picking infrastructure and service'
url='https://github.com/ubports/content-hub'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
depends=(lcov gcovr gmock ubuntu-app-launch-git ubuntu-download-manager-git gsettings-qt apparmor-libapparmor libnotify ubuntu-ui-toolkit-git)
makedepends=(git cmake cmake-extras-git doxygen graphviz) 
checkdepends=(xorg-server-xvfb)
source=('git+https://github.com/ubports/content-hub.git'
        'Gtest.patch'
        'QtArg.patch'
        'GtestSrc.patch')
sha256sums=('SKIP'
            '9fc3692694d916d329a3830e01a4a8c36d897a7e8e2cce2905b4a8d049b9659f'
            '59f927b1cda19e082fce433a852665f26e40f399433caca5fa0e7841d95d48f0'
            '8c360d0af436f19e7bc15a141a10ebaad4e59af9e582b86feaf207071f748849')

BUILDENV+=('!check') # https://paste.ubuntu.com/p/XXm29c8rv8/

pkgver() {
  cd ${_pkgname}
  git describe --always
}

prepare() {
  cd ${_pkgname}
  git checkout bionic
  
  patch -Np1 -i "${srcdir}/Gtest.patch"
  patch -Np1 -i "${srcdir}/QtArg.patch"
  patch -Np1 -i "${srcdir}/GtestSrc.patch"
  sed -i "s/\${GTEST_ALL_LIBRARIES}/\${GMOCK_LIBRARIES}/g" "${srcdir}/${_pkgname}/tests/acceptance-tests/CMakeLists.txt"
}

build() {
  cd ${_pkgname}
  mkdir -p /tmp/runtime-builduser
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  XDG_RUNTIME_DIR=/tmp/runtime-builduser make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
