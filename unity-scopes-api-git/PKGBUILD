# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=unity-scopes-api-git
_pkgname=unity-scopes-api
pkgver=c8275a30
pkgrel=1
pkgdesc='Library to integrate scopes with the Unity shell'
url='https://github.com/ubports/unity-scopes-api'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
depends=(unity-api-git process-cpp apparmor-libapparmor lttng-ust libaccounts-glib libsignon-glib zeromq net-cpp-git json-glib zmqpp capnproto lttng-tools astyle dos2unix clang dbus-test-runner lcov gcovr valgrind cppcheck python-dbusmock python-tornado)
makedepends=(git cmake cmake-extras-git doxygen graphviz)
source=('git+https://github.com/ubports/unity-scopes-api.git'
        'lsb_release.patch'
        'Qt511.patch'
        'Functional.patch'
        'ParseChangelog.patch'
        'RemoveWeirdTest.patch')
sha256sums=('SKIP'
            'a091c80cf1751f8d20c7214ec9f040eddcb80d14d1b5dddff87e9b9446c30b09'
            '0ce7bc0d4204d8355c5edda9295cdb3ba105ced0d9dcc874ab46259febfb927c'
            '3b25236d723254170f9bee5ffc825b05f4d68cf5b5830033c2304ab87250989a'
            '23f846d0e08c8bd1c33c8581d1c96a6558193b4a26b0c7f3b9cbbc7595b3d339'
            '3f360c514def2ab82e66c9284a0af83dd221e968217218b670e49d26ee00b0b5')

BUILDENV+=('!check') # Some tests are broken

BUILD_DIR=build

prepare() {
  cd ${_pkgname}
  git checkout bionic
  
  patch -Np1 -i "${srcdir}/lsb_release.patch"
  patch -Np1 -i "${srcdir}/Qt511.patch"
  patch -Np1 -i "${srcdir}/Functional.patch"
  patch -Np1 -i "${srcdir}/ParseChangelog.patch"
  patch -Np1 -i "${srcdir}/RemoveWeirdTest.patch"
}

pkgver() {
  cd ${_pkgname}
  git describe --always
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  cmake ..
  make
}

check() {
  cd ${_pkgname}/${BUILD_DIR}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
